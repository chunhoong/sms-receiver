plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

repositories {
    jcenter()
    flatDir {
        dirs '../libs'
    }
}

group 'com.chunhoong'
version '1.0.2'

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'

    implementation 'log4j:log4j:1.2.17'
    implementation 'org.slf4j:slf4j-log4j12:1.7.30'

    implementation name: 'smslib-3.5.5'
    implementation 'com.neuronrobotics:nrjavaserial:5.2.1'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'SMS Receiver'
                description = 'Library to read sms from JVM application.'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'chunhoong'
                        name = 'Lim Chun Hoong'
                        email = 'chunhoong.lim@outlook.my'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/chunhoong/sms-receiver.git'
                    developerConnection = 'scm:git:git@github.com:chunhoong/sms-receiver.git'
                    url = 'https://github.com/chunhoong/sms-receiver'
                }
            }
        }
    }
}

bintray {
    user = project.property('bintray.user')
    key = project.property('bintray.key')
    publications = ['shadow']
    pkg {
        repo = 'oss'
        name = 'sms-receiver-lib'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/chunhoong/sms-receiver.git'
    }
}